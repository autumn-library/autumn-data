#Использовать ".."
#Использовать "."
#Использовать autumn

&Тест
Процедура КогдаЗаполненыИсточникиДанныхКакСоответствие_ТогдаВозвращаетсяОдинМенеджерСущностей() Экспорт

	// Дано
	СоветДругогоМастера = ГенераторДанных.СоветДругогоМастераСОднимКоннекторомКакСоответствие();

	Поделка = Новый Поделка(СоветДругогоМастера);
	Поделка.ЗапуститьПриложение();

	// Когда
	МенеджерыСущностей = Поделка.НайтиЖелуди("МенеджерСущностей", , "Соответствие");

	// Тогда
	Ожидаем.Что(МенеджерыСущностей.Количество()).Равно(1);
	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейОсновной"]).ИмеетТип("МенеджерСущностей");
	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейОсновной"].ПолучитьКоннектор()).ИмеетТип("КоннекторInMemory");

КонецПроцедуры

&Тест
Процедура КогдаЗаполненыИсточникиДанныхКакСоответствие_ТогдаВозвращаетсяВерховныйМенеджерСущностей() Экспорт

	// Дано
	СоветДругогоМастера = ГенераторДанных.СоветДругогоМастераСОднимКоннекторомКакСоответствие();

	Поделка = Новый Поделка(СоветДругогоМастера);
	Поделка.ЗапуститьПриложение();

	// Когда
	МенеджерСущностей = Поделка.НайтиЖелудь("МенеджерСущностей");

	// Тогда
	Ожидаем.Что(МенеджерСущностей).ИмеетТип("МенеджерСущностей");
	Ожидаем.Что(МенеджерСущностей.ПолучитьКоннектор()).ИмеетТип("КоннекторInMemory");

КонецПроцедуры

&Тест
Процедура КогдаЗаполненыИсточникиДанныхКакМассив_ТогдаВозвращаетВсеМенеджерыСущностей() Экспорт

	// Дано
	СоветДругогоМастера = ГенераторДанных.СоветДругогоМастераСДвумяКоннекторамиКакМассив();

	Поделка = Новый Поделка(СоветДругогоМастера);
	Поделка.ЗапуститьПриложение();

	// Когда
	МенеджерыСущностей = Поделка.НайтиЖелуди("МенеджерСущностей", , "Соответствие");

	// Тогда
	Ожидаем.Что(МенеджерыСущностей.Количество()).Равно(2);

	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейОсновной"]).ИмеетТип("МенеджерСущностей");
	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейОсновной"].ПолучитьКоннектор()).ИмеетТип("КоннекторInMemory");

	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейДополнительный"]).ИмеетТип("МенеджерСущностей");
	Ожидаем.Что(МенеджерыСущностей["МенеджерСущностейДополнительный"].ПолучитьКоннектор()).ИмеетТип("КоннекторSQLite");

КонецПроцедуры

&Тест
Процедура КогдаЗаполненыИсточникиДанныхКакМассив_ТогдаВозвращаетВерховныйМенеджерСущностей() Экспорт

	// Дано
	СоветДругогоМастера = ГенераторДанных.СоветДругогоМастераСДвумяКоннекторамиКакМассив();

	Поделка = Новый Поделка(СоветДругогоМастера);
	Поделка.ЗапуститьПриложение();

	// Когда
	МенеджерСущностей = Поделка.НайтиЖелудь("МенеджерСущностей");

	// Тогда
	Ожидаем.Что(МенеджерСущностей).ИмеетТип("МенеджерСущностей");
	Ожидаем.Что(МенеджерСущностей.ПолучитьКоннектор()).ИмеетТип("КоннекторInMemory");

КонецПроцедуры
