#Использовать entity

&Пластилин
Перем ТокенайзерЗапросов;

Функция РазобратьИмяМетода(ИмяМетода, ОбъектМодели) Экспорт

	ОпцииПоиска = Новый ОпцииПоиска();

	Идентификаторы = Новый Массив;
	Для Каждого Колонка Из ОбъектМодели.Колонки() Цикл
		Идентификаторы.Добавить(Колонка.ИмяПоля);
	КонецЦикла;

	// TODO: Разобрать имя метода на части
	СписокТокенов = ТокенайзерЗапросов.Разобрать(ИмяМетода, Идентификаторы);
	
	ТокенРежимПоиска = СписокТокенов[0];
	Если ТокенРежимПоиска.ТипТокена() <> "ПолучитьОдно" И ТокенРежимПоиска.ТипТокена() <> "Получить" Тогда
		ВызватьИсключение СтрШаблон(
			"Не удалось разобрать имя метода ""%1"". Первый токен должен быть ""ПолучитьОдно"" или ""Получить"", обнаружен токен %2", 
			ИмяМетода,
			ТокенРежимПоиска.ТипТокена()
		);
	КонецЕсли;

	РежимПоиска = ТокенРежимПоиска.ТипТокена();

	ВидыСравнения = Новый Массив;
	ВидыСравнения.Добавить("Равно");
	ВидыСравнения.Добавить("Больше");
	ВидыСравнения.Добавить("БольшеИлиРавно");
	ВидыСравнения.Добавить("Меньше");
	ВидыСравнения.Добавить("МеньшеИлиРавно");
	ВидыСравнения.Добавить("НеРавно");
	ВидыСравнения.Добавить("В");
	ВидыСравнения.Добавить("НеВ");

	НомерАргумента = 1;
	РежимОтбора = Ложь;
	ОтборПоле = Неопределено;
	РежимУпорядочивания = Ложь;
	УпорядочитьПоПоле = Неопределено; // TODO:

	Для сч = 1 По СписокТокенов.ВГраница() Цикл
		Токен = СписокТокенов[сч];
		Если Токен.ТипТокена() = "Первые" Тогда
			ОпцииПоиска.Первые(НомерАргумента);
			НомерАргумента = НомерАргумента + 1;
		ИначеЕсли Токен.ТипТокена() = "СоСмещением" Тогда
			ОпцииПоиска.Смещение(НомерАргумента);
			НомерАргумента = НомерАргумента + 1;
		ИначеЕсли Токен.ТипТокена() = "Идентификатор" Тогда
			Если РежимУпорядочивания Тогда
				УпорядочитьПоПоле = Токен.Значение();
			Иначе
				РежимОтбора = Истина;
				ОтборПоле = Токен.Значение();
			КонецЕсли;
		ИначеЕсли ВидыСравнения.Найти(Токен.ТипТокена()) <> Неопределено Тогда
			Если НЕ РежимОтбора Тогда
				ВызватьИсключение СтрШаблон(
					"Ошибка разбора имени метода запроса. Вид сравнения ""%1"" указан не после имени поля.",
					Токен.ТипТокена()
				);
			КонецЕсли;
			
			ОпцииПоиска.Отбор(
				ОтборПоле,
				Токен.ТипТокена(),
				НомерАргумента
			);

			РежимОтбора = Ложь;
			НомерАргумента = НомерАргумента + 1;
		ИначеЕсли Токен.ТипТокена() = "УпорядочитьПо" Тогда
			РежимУпорядочивания = Истина;
		ИначеЕсли Токен.ТипТокена() = "Убыв" ИЛИ Токен.ТипТокена() = "Возр" Тогда
			Если НЕ РежимУпорядочивания Тогда
				ВызватьИсключение СтрШаблон(
					"Ошибка разбора имени метода запроса. Режим упорядочивания ""%1"" указан в неожиданной позиции",
					Токен.ТипТокена()
				);
			КонецЕсли;

			ОпцииПоиска.СортироватьПо(
				УпорядочитьПоПоле,
				?(Токен.ТипТокена() = "Убыв", НаправлениеСортировки.Убыв, НаправлениеСортировки.Возр)
			);
		ИначеЕсли Токен.ТипТокена() = "И" Тогда
			// TODO: Завершение режима отбора, вставка вида сравнения "Равно", если он не был установлен.
		КонецЕсли;

	КонецЦикла;

	Результат = Новый Структура;
	Результат.Вставить("РежимПоиска", РежимПоиска);
	Результат.Вставить("ОпцииПоиска", ОпцииПоиска);
	
	Возврат Результат;

КонецФункции

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры