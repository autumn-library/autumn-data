#Использовать entity

Перем НастройкиИсточниковДанныхВходящие;

Функция ИнициализироватьИсточникиДанных()

	ИсточникиДанных = Новый Массив;

	Если ТипЗнч(НастройкиИсточниковДанныхВходящие) = Тип("Соответствие") Тогда
		НастройкиИсточниковДанных = Новый Массив();
		НастройкиИсточниковДанных.Добавить(НастройкиИсточниковДанныхВходящие);
	ИначеЕсли ТипЗнч(НастройкиИсточниковДанныхВходящие) = Тип("Массив") Тогда
		НастройкиИсточниковДанных = НастройкиИсточниковДанныхВходящие;
	Иначе
		Возврат ИсточникиДанных;
	КонецЕсли;

	Для Каждого НастройкаИсточникаДанных Из НастройкиИсточниковДанных Цикл
		ИмяИсточника = НастройкаИсточникаДанных.Получить("Имя");
		ТипКоннектора = Тип(НастройкаИсточникаДанных.Получить("ТипКоннектора"));
		СтрокаСоединения = ПолучитьИли(НастройкаИсточникаДанных, "СтрокаСоединения", "");
		ПараметрыКоннектора = ПолучитьИли(НастройкаИсточникаДанных, "ПараметрыКоннектора", Новый Массив);

		ИсточникДанных = Новый ИсточникДанных(ИмяИсточника, ТипКоннектора, СтрокаСоединения, ПараметрыКоннектора);
		ИсточникиДанных.Добавить(ИсточникДанных);
	КонецЦикла;

	Возврат ИсточникиДанных;

КонецФункции

Функция ПолучитьИли(Соответствие, Ключ, ЗначениеПоУмолчанию)
	Значение = Соответствие.Получить(Ключ);
	Если Значение = Неопределено Тогда
		Значение = ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Значение;
КонецФункции

Процедура ПриИнициализацииПоделки(Поделка) Экспорт

	ИсточникиДанных = ИнициализироватьИсточникиДанных();
	Верховный = Истина;

	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		АннотацияВерховный = ?(Верховный, "&Верховный ", "");
		Поделка.ДобавитьЗавязь(
			АннотацияВерховный + "&Завязь(Тип = ""ИсточникДанных"", Значение = ""ИсточникДанных" + ИсточникДанных.Имя() + """) &Прозвище(""ИсточникДанных"") () -> Возврат ИсточникДанных", 
			Новый Структура("ИсточникДанных", ИсточникДанных)
		);

		// BSLLS:LineLength-off
		Поделка.ДобавитьЗавязь(
			АннотацияВерховный + "&Завязь(Тип = ""МенеджерСущностей"", Значение = ""МенеджерСущностей" + ИсточникДанных.Имя() + """) &Прозвище(""МенеджерСущностей"") () ->
			|   		МенеджерСущностей = Новый МенеджерСущностей(
			|		ИсточникДанных.ТипКоннектора(),
			|		ИсточникДанных.СтрокаСоединения(),
			|		ИсточникДанных.ПараметрыКоннектора()
			|	);
			|	Возврат МенеджерСущностей", 
			Новый Структура("ИсточникДанных", ИсточникДанных)
		);
		// BSLLS:LineLength-on

		Верховный = Ложь;
	КонецЦикла;

КонецПроцедуры

&Заготовка
Процедура ПриСозданииОбъекта(&Деталька("data.ИсточникиДанных") НастройкиИсточниковДанных) Экспорт
	НастройкиИсточниковДанныхВходящие = НастройкиИсточниковДанных;
КонецПроцедуры
