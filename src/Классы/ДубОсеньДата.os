#Использовать entity

&Завязь(Тип = "Массив")
Функция ИсточникиДанных(&Деталька("data.ИсточникиДанных") НастройкиИсточниковДанныхВходящие) Экспорт

	ИсточникиДанных = Новый Массив;

	Если ТипЗнч(НастройкиИсточниковДанныхВходящие) = Тип("Соответствие") Тогда
		НастройкиИсточниковДанных = Новый Массив();
		НастройкиИсточниковДанных.Добавить(НастройкиИсточниковДанныхВходящие);
	ИначеЕсли ТипЗнч(НастройкиИсточниковДанныхВходящие) = Тип("Массив") Тогда
		НастройкиИсточниковДанных = НастройкиИсточниковДанныхВходящие;
	Иначе
		ВызватьИсключение "Неверно заполнены настройки секции data.ИсточникиДанных";
	КонецЕсли;


	Для Каждого НастройкаИсточникаДанных Из НастройкиИсточниковДанных Цикл
		ИмяИсточника = НастройкаИсточникаДанных.Получить("Имя");
		ТипКоннектора = Тип(НастройкаИсточникаДанных.Получить("ТипКоннектора"));
		СтрокаСоединения = ПолучитьИли(НастройкаИсточникаДанных, "СтрокаСоединения", "");
		ПараметрыКоннектора = ПолучитьИли(НастройкаИсточникаДанных, "ПараметрыКоннектора", Новый Массив);

		ИсточникДанных = Новый ИсточникДанных(ИмяИсточника, ТипКоннектора, СтрокаСоединения, ПараметрыКоннектора);
		ИсточникиДанных.Добавить(ИсточникДанных);
	КонецЦикла;

	Возврат ИсточникиДанных;

КонецФункции

&Верховный
&Завязь
Функция ИсточникДанных(&Пластилин ИсточникиДанных) Экспорт

	Возврат ИсточникиДанных[0];

КонецФункции

&Завязь(Тип = "Массив")
Функция МенеджерыСущностей(&Пластилин ИсточникиДанных) Экспорт
	
	МенеджерыСущностей = Новый Массив;

	Для Каждого ИсточникДанных Из ИсточникиДанных Цикл
		МенеджерСущностей = Новый МенеджерСущностей(
			ИсточникДанных.ТипКоннектора(),
			ИсточникДанных.СтрокаСоединения(),
			ИсточникДанных.ПараметрыКоннектора()
		);
		МенеджерыСущностей.Добавить(МенеджерСущностей);
	КонецЦикла;

	Возврат МенеджерыСущностей;

КонецФункции

&Верховный
&Завязь
Функция МенеджерСущностей(&Пластилин МенеджерыСущностей) Экспорт

	Возврат МенеджерыСущностей[0];

КонецФункции

Функция ПолучитьИли(Соответствие, Ключ, ЗначениеПоУмолчанию)
	Значение = Соответствие.Получить(Ключ);
	Если Значение = Неопределено Тогда
		Значение = ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Значение;
КонецФункции

&Дуб
Процедура ПриСозданииОбъекта()
КонецПроцедуры
