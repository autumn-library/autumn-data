#Использовать extends

Перем _Поделка;
Перем _КонтейнерАннотаций;

Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт

	Конструктор = ОпределениеЖелудя.Завязь().ДанныеМетода();
	АннотацияХранилищеСущностей = РаботаСАннотациями.НайтиАннотацию(Конструктор.Аннотации, "ХранилищеСущностей");

	Если АннотацияХранилищеСущностей = Неопределено Тогда
		Возврат Желудь;
	КонецЕсли;

	ИмяТипаСущности = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияХранилищеСущностей, "Значение");
	ИсточникДанныхСущности = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияХранилищеСущностей, "ИсточникДанных", "");

	ХранилищеСущностей = _Поделка.НайтиЖелудь("ХранилищеСущностей" + ИсточникДанныхСущности + ИмяТипаСущности);

	ПостроительНаследника = Новый ПостроительНаследника(Желудь, ХранилищеСущностей, ОпределениеЖелудя.Методы(), ОпределениеЖелудя.Завязь().ДанныеМетода());	
	ПостроительНаследника.УстановитьКонтейнерАннотаций(_КонтейнерАннотаций);

	Возврат ПостроительНаследника.Построить();

КонецФункции

&Напильник
Процедура ПриСозданииОбъекта(&Пластилин Поделка)
	_Поделка = Поделка;
	// TODO: Когда-нибудь это можно будет решить пластилином желудей времени инициализации.
	_КонтейнерАннотаций = _Поделка.НайтиЖелудь("КонтейнерАннотаций");
КонецПроцедуры